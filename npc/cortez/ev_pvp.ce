////////Autor////////////////////////////////////////////
//= Gui meira e TheBusiness
////////NPC//////////////////////////////////////////////
//= NPC Multi Função
////////Emulador/////////////////////////////////////////
//= eathena sql
////////Comentarios adicionais///////////////////////////
//= Obrigado ao DEX por ser o nossso mentor e o mestre nos
//= NPCs e a nós dois (eu e gui) por conseguir fazer este
//= NPC.
/////////////////////////////////////////////////////////

guild_vs5	mapflag	nosave	SavePoint
guild_vs5	mapflag	nomemo
guild_vs5	mapflag	nopenalty
guild_vs5	mapflag	nosave
guild_vs5	mapflag	restricted	4
guild_vs5	mapflag	nodrop
guild_vs5	mapflag	nowarpto
guild_vs5	mapflag	pvp_noparty
guild_vs5	mapflag	pvp_noguild

new_1-1,47,117,5	script	Senhor do PvP#1	52,{
	if(getgmlevel()<59) close;
	if ($eventopvp == 1) goto EVENTOACONTECENDO;
	if(getgmlevel()<80){
		mes "[Senhor do PvP]";
		mes "Olá Sr.GM!";
		mes "O que acha de iniciar um evento PvP?";
		next;
		menu "Iniciar!",INICIAR2,"Cancelar",CANCEL;

		INICIAR2:
			mes "[Senhor do PvP]";
			mes "O premio atual que deve ser dado em um Evento PVP sao Duas Ticket de Evento.";
			next;
			set $tipopremio,1; 	//Tipo=Itens	Se não for dar itens, trocar depois
			set $quantpremio,1;	//Qtde=1		Se for mais, mudar depois
			menu "Cancelar",CANCEL,"Ticket de Evento",PR_PVP;
		
		PR_PVP:
			set $idpremio,7180;
			set $nomepremio$,"Ticket de Evento";
			set $quantpremio,2;
			goto LEVELEVENTO;
		
		
	}else{
		mes "[Senhor do PvP]";
		mes "Ola senhor GM.";
		mes "O que acha de iniciar um evento PvP?";
		next;
		menu "Boa idéia!",INICIAR,"Quem sabe depois?",CANCEL;

		INICIAR:
		mes "[Senhor do PvP]";
		mes "O que vai dar ao jogador como premio?";
		next;
		menu "Prêmios Predefinidos",INICIAR2,"Itens",ITENS,"Zenys",ZENYS,"Nada!",NADA,"Eu premiarei o campeão",PREMIOGM;

		ITENS:
		mes "[Senhor do PvP]";
		mes "Por favor, digite o ID do item que sera o premio:";
		input $idpremio;
		next;
		mes "[Senhor do PvP]";
		mes "Agora digite o nome do item:";
		input $nomepremio$;
		next;
		mes "[Senhor do PvP]";
		mes "Agora digite a quantidade desse item que o jogador recebera:";
		input $quantpremio;
		set $tipopremio,1;
		next;
		goto LEVELEVENTO;

		ZENYS:
		mes "[Senhor do PvP]";
		mes "Por favor, digite a quantidade de zenys que o jogador receberá (multiplos de 1 milhão):";
		input $quantpremio;
		next;
		if(($quantpremio/1000000)*1000000 != $quantpremio) goto ZENYS;
		set $tipopremio,2;
		goto LEVELEVENTO;
		
		NADA:
		set $tipopremio,3;
		goto LEVELEVENTO;
		
		PREMIOGM:
		set $tipopremio,4;
		goto LEVELEVENTO;
	}
	
	LEVELEVENTO:
	mes "[Senhor do PvP]";
	mes "Qual o nº de resets mínimo para que participem deste evento?";
	mes "Coloque 0 para nao haver mínimo";
	input $levelminimo;
	next;
	mes "[Senhor do PvP]";
	mes "Qual o nº de resets máximo para que participem deste evento?";
	mes "Coloque 12 para nao haver máximo";
	input $levelmaximo;
	if($levelmaximo>12 || $levelmaximo<0) set $levelmaximo,12;
	next;
	mes "[Senhor do PvP]";
	mes "Evento configurado com sucesso!";
	set $idGmEvento,getcharid(3);
	close2;
	goto CONTROLEEVENTO;
	end;

	CANCEL:
	mes "[Senhor do PvP]";
	mes "Ok, volte aqui se mudar de ideia!";
	close;
	EVENTOACONTECENDO:
	mes "[Senhor do PvP]";
	mes "Me desculpe mas ja existe um evento PvP acontecendo!";
	mes "Há "+(getmapusers("guild_vs5"))+" jogadores vivos no mapa!";
	menu "Ok.",FECHANDO,"Ir para o mapa",-,"Cancelar Evento",M_CANCELEVENTO;
	warp "guild_vs5",0,0;
	end;
	
	M_CANCELEVENTO:
	announce "O Evento PvP foi cancelado!!!",0;
	disablenpc "Senhor do PvP#2";
	stopnpctimer;
	set $idpremio,0;
	set $nomepremio$,0;
	set $quantpremio,0;
	set $tipopremio,0;
	set $eventopvp,0;
	set $levelminimo,0;
	set $levelmaximo,12;
	
	FECHANDO:
	close;

	CONTROLEEVENTO:
	initnpctimer;
	set $eventopvp,1;
	announce "Ola aventureiros de RuneMidgard! Aqui é o Senhor do PvP!",0;
	//killmonsterall "guild_vs5";
	end;
	
	OnTimer15000:
	announce "Está iniciado mais um Evento PvP!",0;
	end;
	
	OnTimer30000:
	if($tipopremio==1){
		announce "O prêmio será 2 Tickets de Evento!",0;
		set $@premio$,$quantpremio+" "+$nomepremio$;
	}else if($tipopremio==2){
		announce "O prêmio será "+($quantpremio/1000000)+" milhões de zeny!",0;
		set $@premio$,($quantpremio/1000000)+" milhões de zeny";
	}else if($tipopremio==3){
		announce "Neste evento não haverá premiacão!",0;
		set $@premio$,"Nenhum";
	}else if($tipopremio==4){
		announce "O prêmio sera entregue pelo GM em pessoa!",0;
		set $@premio$,"Entregue pelo GM";
	}
	if(attachrid($idGmEvento))
		logmes "[Evento PVP] Prêmio "+$@premio$+" para resets "+$levelminimo+" a "+$levelmaximo;
	else
		query_logsql "INSERT INTO npclog (npc_date,account_id,char_id,char_name,map,mes) Values (now(),1,150000,'[GM] id "+$idGmEvento+"','new_zone01','[Evento PVP] Prêmio "+$@premio$+" para resets "+$levelminimo+" a "+$levelmaximo+"')";
	end;
	
	OnTimer45000:
	if(($levelminimo == 0)&&($levelmaximo == 12)){ announce "Jogadores de qualquer nível e resets podem participar deste evento!",0; end; }
	if(($levelminimo == $levelmaximo)){
		if($levelminimo==0)
			announce "Este evento é exclusivo para personagens iniciantes, sem reset!",0;
		else
			announce "É preciso ter exatamente "+$levelminimo+" resets para participar deste evento!",0;
		end;
	}
	if ($levelminimo == 0) announce "Não há mínimo de resets para participar deste evento!",0;
	else if ($levelminimo > 0) announce "O nº de resets mínimo para participar deste evento é "+$levelminimo+"!",0;
	end;
	
	OnTimer60000:
	if((($levelminimo == 0)&&($levelmaximo == 12))||($levelminimo == $levelmaximo))
		end;
	else if($levelmaximo==12)
		announce "Não há máximo de resets máximo para participar deste evento!",0;
	else if($levelmaximo>0)
		announce "E o nº de resets máximo para participar deste evento é "+$levelmaximo+"!",0;
	end;

	OnTimer75000:
	announce "Os interessados em participar me procurem no centro da cidade para se inscreverem.",0;
	announce "Eu só ficarei lá 5 minutos!",0;
	enablenpc "Senhor do PvP#2";
	disablenpc "Senhor do PvP#3";
	end;

	OnTimer255000:
	announce "O evento irá começar em dois minutos!!! Me encontre no centro da cidade!",0;
	if($tipopremio<=2)
		announce "Venha concorrer a "+$@premio$+" no Evento PvP!",0;
	end;
	
	OnTimer365000:
	if($tipopremio<=2)
		announce "10 segundos para começar o Evento PvP! Quem ganhará "+$@premio$+"?",0;
	end;
	
	OnTimer375000:
	if (getmapusers("guild_vs5") < 2) goto CANCELEVENTO;
	announce "Inscrições encerradas! O evento começa agora com "+getmapusers("guild_vs5")+" competidores!",0;
	announce "Quem sobreviver ganha o prêmio! Que vença o melhor!",0;
	disablenpc "Senhor do PvP#2";
	enablenpc "Senhor do PvP#3";
	pvpon "guild_vs5";
	end;
	
	OnTimer675000:
	if(getmapusers("guild_vs5")>1)
		announce "Temos "+getmapusers("guild_vs5")+" lutadores vivos no Evento PvP, lutando por "+$@premio$+"!",0;
	stopnpctimer;
	end;
	
	CANCELEVENTO:
	announce "O Evento PvP foi cancelado por falta de jogadores!",0;
	stopnpctimer;
	disablenpc "Senhor do PvP#2";
	set $idpremio,0;
	set $nomepremio$,0;
	set $quantpremio,0;
	set $tipopremio,0;
	set $eventopvp,0;
	set $levelminimo,0;
	set $levelmaximo,12;
	end;
}

gonryun,156,115,5	script	Senhor do PvP#2	52,{
	if ($eventopvp == 0) goto SEMEVENTO;
	if (reborn == 0) set reborn, 1;
	if (reborn-1 < $levelminimo || reborn-1 > $levelmaximo) goto SEMLEVEL;
	mes "[Senhor do PvP]";
	mes "Olá! Veio se inscrever no Evento PvP?";
	mes "Você tem "+(reborn-1)+" resets, portanto pode participar!";
	next;
	menu "Sim!",INSCREVER,"Não...",CANCEL;
	
	INSCREVER:
	mes "[Senhor do PvP]";
	mes "Prontinho! Voce está inscrito no Evento! Aguarde a hora da luta dentro da arena e boa sorte!";
	close2;
	warp "guild_vs5",0,0;
	end;

	SEMEVENTO:
	//mes "[Senhor do PvP]";
	//mes "No momento não está acontecendo nenhum Evento PvP!";
	//mes "Na verdade eu nem deveria estar aqui, por favor comunique-se com um GM!";
	donpcevent "AntiBug::OnInit";
	close;

	SEMLEVEL:
	mes "[Senhor do PvP]";
	if((reborn-1) < $levelminimo){
		mes "Me desculpe mas é necessário pelo menos "+$levelminimo+" resets para participar!";
		if(reborn==1){
			mes "Você ainda não tem nenhum...";
		}else{
			mes "Você tem "+(reborn-1)+" atualmente...";
		}
	} else {
		if((reborn-1) > $levelmaximo){
			if($levelmaximo==0){
				mes "Me desculpe mas não se pode entrar se você tiver resets, e você já tem "+(reborn-1)+"!";
			}else{
				mes "Me desculpe mas não se pode entrar se você tiver pelo menos "+$levelmaximo+" resets!";
				mes "Você já tem "+(reborn-1)+" resets!";
			}
		}
	}
	close;

	CANCEL:
	mes "[Senhor do PvP]";
	mes "Ok, volte aqui se mudar de idéia, mas decida rápido antes que o tempo acabe!";
	close;
}

guild_vs5,50,53,4	script	Senhor do PvP#3	52,{
	callfunc "premio_eventoPvp";
}

function	script	premio_eventoPvp	{
	if(!$eventopvp) end;
	
	mes "[Senhor do PvP]";
	if (getmapusers("guild_vs5")>1){
		mes "Só falo com campeões!!!";
		close;
	}
	mes "Vejo que voce foi o campeão desse evento!";
	mes "Meus parabéns meu jovem!!!!";
	logmes "[Evento PVP] "+strcharinfo(0)+" foi o campeão";
	next;
	mes "[Senhor do PvP]";
	switch($tipopremio){
		case 1: //item
			getitem $idpremio,$quantpremio;
			mes "Aqui está o seu premio! Meus parabéns!";
			break;
		case 2:	//zeny
			query_logsql "INSERT INTO zenylog(time,char_id,src_id,type,amount,map)VALUES(now(),"+getcharid(0)+","+getcharid(0)+",'N',"+$quantpremio+",'evento_pvp')";
			set Zeny,Zeny+$quantpremio;
			mes "Aqui está o seu premio! Meus parabéns!";
			break;
		case 3:	//Nada
			mes "O importante e competir, e este evento não estava valendo nenhum prêmio! Mesmo assim parabéns!";
			break;
		case 4: //GM
			mes "O GM lhe chamará no castelo daqui a pouco para que você receba o seu prêmio!";
			break;
	}
	
	mes "Agora volte para a cidade!";
	close2;
	announce strcharinfo(0)+" ganhou o evento de pvp! Parabéns!!!",0;
	warp "SavePoint",0,0;

	set $idpremio,0;
	set $nomepremio$,0;
	set $quantpremio,0;
	set $tipopremio,0;
	set $eventopvp,0;
	set $levelminimo,0;
	set $levelmaximo,0;
	disablenpc "Senhor do PvP#2";
	pvpoff "guild_vs5";
	return;
}

-	script	AntiBug	-1,{
	OnInit:
	disablenpc "Senhor do PvP#2";
	removemapflag "guild_vs5",6;
	set $idpremio,0;
	set $nomepremio$,0;
	set $quantpremio,0;
	set $tipopremio,0;
	set $eventopvp,0;
	set $levelminimo,0;
	set $levelmaximo,0;
	end;
}
