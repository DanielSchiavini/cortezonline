//===== Cronus Script ========================================
//= Quests de Izlude
//===== Por: =================================================
//= Cainho
//===== Versão Atual: ========================================
//= 1.1
//===== Compatível Com: ======================================
//= Emulador [3010+]; Cliente [EP 12.1]
//===== Descrição: ===========================================
//= [Conversão do Aegis]
//= Quests da cidade de Izlude
//===== Comentários Adicionais: ==============================
//= 1.0 Conversão do Aegis [Cainho]
//= 1.1 Revisado e otimizado [Mara]
//============================================================

izlude,182,186,2	script	Edgar	49,{
	
	if (!iz_move_alberta)
	{
		mes "[Edgar]";
		mes "Minha cidade, Izlude, está conectada a Alberta pelo ancoradouro a Oeste. Há muito tráfego entre as cidades, então eu quase sou de Alberta e não de Izlude. Hehehe.";
		mes "Eu conheço muito bem aquele sujeito Phelix de Alberta, aquele cara é muito exigente, então é sem almoço grátis com ele.";
		next;
		mes "[Edgar]";
		mes "Mas ele é um cara legal, ele gosta de ajudar aos outros. Ele tem um grande coração, ele te ajudará bastante se você atender as condições que ele pede.";
		next;
		mes "[Edgar]";
		mes "Ultimamente as pessoas de Alberta dizem que ele vem ajudando pessoas que lhe trazem uma grande quantidade de Jellopy ou algo parecido.. se você estiver com o bolso vazio, é melhor dar uma olhada no que ele quer.";
		next;
		if (select ("Pode me dizer o caminho para Alberta?:Fim de conversa") == 1)
		{
			mes "[Edgar]";
			mes "Como? Você pode ir andando, mas se você tiver dinheiro eu sugiro que você pegue um barco.";
			next;
			if (select ("Ok entendi.:Cansado de andar e sem dinheiro.") == 1)
			{
				mes "[Edgar]";
				mes "Tudo bem, cuide-se...";
				close;
			}
			mes "[Edgar]";
			mes "Cansado de andar e sem dinheiro? Meu amigo..";
			mes "Certo, certo, como capitão do navio eu posso te levar a um preço menor, 250 Zeny.";
			next;
			if (select ("Legal!!!:Que extorsão!!") == 1)
			{
				set iz_move_alberta, 1;
				callsub F_CheckZeny;
			}
			mes "[Edgar]";
			mes "Ou isso ou vá andando...";
			close;
		}
		mes "[Edgar]";
		mes "Vá cuidar da sua vida então...";
		close;
	}
	mes "[Edgar]";
	mes "Então quer ir para Alberta de novo?";
	mes "Vou cobrá-lo 250 Zeny como da última vez.";
	next;
	mes "[Edgar]";
	mes "Que tal?";
	next;
	if (select ("Legal!!!:Mentiroso!!") == 1) callsub F_CheckZeny;
	mes "[Edgar]";
	mes "Boa sorte!!!";
	close;
	
	F_CheckZeny:
		if (Zeny < 250)
		{
			mes "[Edgar]";
			mes "Mas você não tem dinheiro o suficiente! Vá conseguir mais.";
			close;
		}
		set Zeny,Zeny-250;
		warp "alberta",195,164;
		end;
	return;
}
