//===== Cronus Script ========================================
//= Guerra do Emperium - Início Automático da GdE SE
//===== Por: =================================================
//= kalen (1.0)
//= 1.1 by Akaru and ho|yAnge|
//===== Versão Atual: ========================================
//= 2.4c
//===== Compatível com: ======================================
//= Cronus SVN; Episódio RO 4+
//===== Descrição: ===========================================
//= Início Automático da Guerra do Emperium
//= Para configuração do mesmo, leia por favor o arquivo em doc\woe_time_explanation.txt
//= Tempo de Início/Fim da WoE 
//===== Comentários Adicionais: ==============================
//= 2.0 Trazido do eAthena. [Asus]
//= 2.1 Adicionado announce oficial do bRO. [SoulBlaker]
//= 2.2 Modificação de configuração de horário e preparamento para quests futuras. [Reply.]
//= 2.2a Correção no Script, não pode usar duas labels iguals. [RoM]
//= 2.3 Reestruturado. [RoM]
//= 2.3b Alterado de label para função [Heaven]
//= 2.4 Editado para atender à WoE SE [Heaven]
//= 2.4b Consertado BUG correspondente ao final da WoE [Heaven]
//= 2.4c Pequenas modificações aleatórias. [Mara]
//============================================================
-	script	Agit_Event_SE	-1,{
	end;

OnInit:
OnMinute00:
OnTimer60000:
	stopnpctimer;
	if (gettime(3) == $agit_hora_start_se[gettime(4)] && gettime(2) == $agit_min_start_se[gettime(4)] && !agitcheck2())
	{
		if (!agitcheck())
		{
			announce "O Período de Guerras da Segunda Edição começou!", 8;
			announce "Desejamos boa sorte a todos!", 8;
			agitstart2;
			callsub OnDisplayOwners_SE;
		}
	}
	else if (gettime(3) == $agit_hora_end_se[gettime(4)] && gettime(2) == $agit_min_end_se[gettime(4)] && agitcheck2())
	{
		if (!agitcheck())
		{ 
			announce "Neste momento está encerrado o Período de Guerras da Segunda Edição.", 8;
			announce "Obrigado a todos que participaram!", 8;
			agitend2;
			callsub OnDisplayOwners_SE;
		}
	}
	initnpctimer;
	end;

	OnDisplayOwners_SE:
		setarray .@maps_se$[0], "arug_cas01","arug_cas02","arug_cas03","arug_cas04","arug_cas05",	// Feudo de Arunafeltz
								"schg_cas01","schg_cas02","schg_cas03","schg_cas04","schg_cas05";	// Feudo de Schwarzwald
		for (set .@i, 0; .@i <= getarraysize(.@maps_se$); set .@i, .@i + 1)
		{
			if (getcastledata(.@maps_se$[.@i],1))
				announce "O Castelo de [" + getcastlename(.@maps_se$[.@i]) + "] foi conquistado pelo Clã [" + getguildname(getcastledata(.@maps_se$[.@i],1)) + "].", 48;
			else
				announce "O Castelo de [" + getcastlename(.@maps_se$[.@i]) + "] está totalmente desocupado.", 48;	
		}
	return;
}