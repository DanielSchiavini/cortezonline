//===== Cronus Script ======================================== 
//= Guias da Cidade de Prontera
//===== Por: ================================================= 
//= Mara
//===== Versão: ============================================== 
//= 1.1
//===== Compatível com: ====================================== 
//= Emulador [3010]; Cliente [EP 12.1]
//===== Descrição: =========================================== 
//= [Conversão do Aegis]
//===== Comentários adicionais: ============================== 
//= 1.0 Conversão do Aegis [Mara]
//= 1.1 Revisado e otimizado [Mara]
//============================================================ 

prontera,154,187,4	script	Guia#prt1::gPrt	105,{
	mes "[Guia de Prontera]";
	mes "Bem-vind"+(Sex?"o":"a")+" a Prontera, capital de Midgard.";
	mes "Nós estamos aqui para ajudar os visitantes da cidade. Se você tem alguma dúvida, consulte-nos.";
	mes "Se esta é a sua primeira vez utilizando um guia, por favor selecione 'Tutorial'.";
	while (1)
	{
		next;
		switch (select ("Indicações","Limpar indicações","Tutorial","Cancelar."))
		{
			case 1:
				mes "[Guia de Prontera]";
				mes "Por favor, selecione uma das opções.";
				if (.@compass_check == 0)
				{
					mes "Você gostaria que as indicações permaneçam no mini-mapa?";
					if (select ("Sim.","Não.") == 1)
    						set .@compass_check, 1;
    					else
						set .@compass_check, 0;
				}
				while (1)
				{					
					next;
					switch (select ("Associação dos Espadachins","^0000FFCatedral^000000","Cavalaria de Prontera","Loja de Armas","Loja de Ferramentas","Estalagem","Posto de Trocas","Bar","Biblioteca","Agência de Empregos","Castelo de Prontera","Prefeitura","Cancelar"))
					{
					
						case 1:
							mes "[Guia de Prontera]";
							mes "A Associação dos Espadachins, aonde pode-se tornar um deles, foi mudada para a nossa cidade satelite, Izlude. Agora temos apenas um prédio vazio";
							if (.@compass_check == 1)
								viewpoint 1,237,41,4,0x00FF00;												
							break;
						
						case 2:
							mes "[Guia de Prontera]";
							mes "A 'Catedral' se encontra na ponta nordeste da cidade, lá é possivel se tornar um Padre.";
							if (.@compass_check == 1)
								viewpoint 1,236,316,5,0xFF0000;
							break;
						
						case 3:
							mes "[Guia de Prontera]";
							mes "A 'Cavalaria de Prontera' se encontra na ponta nordeste da cidade. É lá que Espadachins se tornam Cavaleiros.";
							if (.@compass_check == 1)
								viewpoint 1,46,345,6,0x00FF00;								
							break;
													
						case 4:
							mes "[Guia de Prontera]";						
							mes "A 'Loja de Armas' se encontra ao nordeste da fonte, no centro da cidade.";
							if (.@compass_check == 1)
								viewpoint 1,175,220,7,0xFF00FF;								
							break;
						
						case 5:
							mes "[Guia de Prontera]";
							mes "A 'Loja de Ferramentas' se encontra a noroeste da fonte, no centro da cidade.";
							if (.@compass_check == 1)
								viewpoint 1,134,221,8,0xFF00FF;
							break;
						
						case 6:
							mes "[Guia de Prontera]";
							mes "A 'Estalagem' se encontra tanto do lado leste quando do oeste da fonte, no centro da cidade.";
							if (.@compass_check == 1)
							{
								viewpoint 1,204,214,9,0xFF00FF;
								viewpoint 1,107,192,10,0xFF00FF;
							}									
							break;
						
						case 7:
							mes "[Guia de Prontera]";
							mes "O 'Posto de Troca' se encontra ao sudeste da fonte, no centro da cidade.";
							if (.@compass_check == 1)
								viewpoint 1,179,184,11,0x00FF00;	
							break;
						
						case 8:
							mes "[Guia de Prontera]";
							mes "O 'Bar' se encontra ao sudeste da fonte, atrás do Posto de Troca, no centro da cidade.";
							if (.@compass_check == 1)
								viewpoint 1,208,154,12,0x00FF00;
							break;
						
						case 9:
							mes "[Guia de Prontera]";
							mes "Se você for um pouco para o nordeste, a partir da fonte no centro da cidade, você encontrará os dois prédios da 'Biblioteca'";
							if (.@compass_check == 1)
							{
								viewpoint 1,120,267,13,0x00FF00;
								viewpoint 1,192,267,14,0x00FF00;
							}								
							break;
						
						case 10:
							mes "[Guia de Prontera]";
							mes "A 'Agencia de Empregos' se encontra a sudoeste da fonte, no centro da cidade.";
							if (.@compass_check == 1)
								viewpoint 1,133,183,15,0x00FF00;
							break;
						
						case 11:
							mes "[Guia de Prontera]";
							mes "O 'Castelo de Prontera' se encontra no extremo norte da  cidade.";
							mes "Você pode atravessar o Castelo, você chegará ao campos nortes.";
							if (.@compass_check == 1)
								viewpoint 1,156,360,16,0x00FF00;
							break;
						
						case 12:
							mes "[Guia de Prontera]";
							mes " A 'Prefeitura da Cidade' se encontra na ponta sudoeste da cidade.";
							if (.@compass_check == 1)
								viewpoint 1,75,91,17,0x01FF01;
							break;
						
						case 13:
							set .@compass_check, 1;
							mes "[Guia de Prontera]";
							mes "Se você deseja apagar as indicações do seu mini-mapa, por favor selecione 'Limpar indicações'.";						
							close;
					}
				}
				break;
				
			case 2:
				viewpoint 2,237,41,4,0x00FF00;
				viewpoint 2,237,41,5,0xFF0000;
				viewpoint 2,46,345,6,0x00FF00;
				viewpoint 2,175,220,7,0xFF00FF;
				viewpoint 2,134,221,8,0xFF00FF;
				viewpoint 2,204,214,9,0xFF00FF;
				viewpoint 2,107,192,10,0xFF00FF;
				viewpoint 2,179,184,11,0x00FF00;
				viewpoint 2,208,154,12,0x00FF00;
				viewpoint 2,120,267,13,0x00FF00;
				viewpoint 2,192,267,14,0x00FF00;	
				viewpoint 2,133,183,15,0x00FF00;
				viewpoint 2,156,360,16,0x00FF00;
				viewpoint 2,75,91,17,0x01FF01;
				set .@compass_check, 0;
				break;
				
			case 3:
				mes "[Guia de Prontera]";
				mes "Nós fomos exaustivamente treinados para servi-l"+(Sex?"o":"a")+" da melhor maneira possivel e sem falhas.";
				mes "Assim você tem a garantia das melhores indicações possiveis.";
				next;	
				mes "[Guia de Prontera]";
				mes "As indicações que lhe daremos podem ser marcadas no seu mini-mapa.";
				mes "Para visualizar o seu mini-mapa, aperte 'ctrl+tab', ou clique no botão 'mapa' no menu principal.";
				mes "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
				break;
				
			case 4:
				mes "[Guia de Prontera]";
				mes "Espero que você tenha uma jornada segura e aproveite sua estadia.";
				close;
	
		}
	}
}

prontera,282,208,2	duplicate(gPrt)	Guia#prt2	105
prontera,29,200,6	duplicate(gPrt)	Guia#prt3	105
prontera,160,29,0	duplicate(gPrt)	Guia#prt4	105
prontera,151,330,4	duplicate(gPrt)	Guia#prt5	105