//===== Cronus Script ========================================
//= Mestra das Classes
//===== Por: =================================================
//= Reply.
//===== Versão: ==============================================
//= 1.5
//===== Descrição: ===========================================
//= Muda a classe do personagem.
//===== Changelog: ===========================================
//= 1.0 - NPC Criado. [Reply.]
//= 1.1 - Corrigido alguns erros. [Reply.]
//= 1.2 - Corrigido a checagem de job para Baby Aprendiz. [RoM]
//=     - Corrigido onde mostra o nome na função.
//= 1.3 - Adicionada uma verificação de peso ao rebornar para não perder itens. [RoM]
//= 1.3a - Algumas correções de gramática. [Puncker]
//= 1.3b - Uma pequena correção. [RoM]
//= 1.3c - Corrigido erro de "Frase desnecessária" [Heaven]
//= 1.3d - Não há necessidade de usar uma variável no nome do npc, retirado. [Mooka]
//= 1.3e - Corrição no if() da função checkconditions(), problema com classes baby. [Mooka]
//= 1.3f - Removido show [Keoy]
//= 1.4 - Refeito NPC. [Mara]
//= 1.5 - Adicionada classes 3-1 e 3-2. [Mara]
//============================================================
prontera,153,193,4	script	Mestra das Classes	864,{

	mes "[Mestra das Classes]";
	if (Class > 22 && Class < 26 || Class > 4029 && Class < 4046 || Class > 4046 && Class < 4080)
	{
		mes "Que bom te ver de novo "+strcharinfo(0)+"!";
		mes "Você está bem forte, parabéns.";
		close;
	}
	if (SkillPoint)
		callsub S_MesClose, "Você precisa utilizar todos os seus pontos de habilidade antes de mudar de classe.";
	if (checkcart() || checkfalcon() || checkriding() || checkmado() || checkwarg())
		callsub S_MesClose, "Você não pode mudar de classe enquanto estiver utilizando Falcão, Carrinho ou PecoPeco.";
	if (!Class || Class == 4023)
	{
		callsub F_ReqBaseJobLevel, 10;
		mes "Olá "+strcharinfo(0)+", vejo que você é apenas um "+jobname(Class)+"!";
		mes "Você está apto a mudar de classe. Qual deseja se tornar?";
		next;
		if (!Class)
		{
			select ("Espadachim:Mago:Arqueiro:Noviço:Mercador:Gatuno:Taekwon:Justiceiro:Ninja:Super Aprendiz");
			set .@chg, ((@menu == 7) ? 4046 : ((@menu == 8 || @menu == 9) ? (16+@menu) : ((@menu == 10) ? 23 : @menu)));
		}
		else
		{
			select ("Mini Espadachim:Mini Mago:Mini Arqueiro:Mini Noviço:Mini Mercador:Mini Gatuno:Mini Super Aprendiz");
			set .@chg, ((@menu == 7) ? 4045 : (4023+@menu));
		}
		callsub F_ChangeJob, set (lastjob1, .@chg);
	}
	else if (Class > 0 && (Class < 7 || Class == 4046))
	{
		callsub F_ReqBaseJobLevel, 40;
		mes "Vejo que você voltou; e mais forte!";
		mes "Pois bem, que classe deseja seguir agora?";
		switch (Class)
		{
			case 1:
					setarray .@c[0], 7, 14;
					break;
					
			case 2:
					setarray .@c[0], 9, 16;
					break;
					
			case 3:
					setarray .@c[0], 11, ((Sex)?19:20);
					break;
					
			case 4:
					setarray .@c[0], 8, 15;
					break;
			
			case 5:
					setarray .@c[0], 10, 18;
					break;
					
			case 6:
					setarray .@c[0], 12, 17;
					break;
					
			case 4046:
					setarray .@c[0], 4047, 4049;
					break;
		}
		next;
		select (jobname(.@c[0])+"",""+jobname(.@c[1]));
		callsub F_ChangeJob, set(lastjob, .@c[(@menu-1)]);
	}
	else if (Class > 6 && Class < 22)
	{
		callsub F_ReqBaseJobLevel, 50, BaseLevel;
		mes "Vejo que você está bastante forte!";
		mes "Qual profissão você quer seguir?";
		next;
		if (select ("Desejo rebornar:Quer me tornar uma classe 3!") == 2)
		{
			mes "[Mestra das Classes]";
			mes "^FF0000Debug: relogue após se transformar para evitar transtornos.^000000";
			next;
			setarray .@ard[0], 4054, 4057, 4055, 4058, 4056, 4059, 4066, 4070, 4067, 4072, 4071, 4069, 4068;
			set .@rd, .@ard[(Class-((Class < 13)?7:8))];
			callsub F_ChangeJob, .@rd;
		}
		mes "[Mestra das Classes]";
		mes "Gostaria de renascer?";
		next;
		if (select ("Não:Sim") == 1)
			close;
		mes "[Mestra das Classes]";
		if (Weight > 1500)
			callsub S_MesClose, "Você está acima do peso permitido, você precisa ter menos que 1500.";
		jobchange 4001;
		resetlvl(1);
		mes "Você ganhará 100 Pontos de Atributo, e algumas habilidades";
		mes "para ajudar-lo em sua jornada.";
		close;
	}
	else if (Class == 4001)
	{
		callsub F_ReqBaseJobLevel, 10;
		mes "Você voltou tão rápido e está evoluindo muito ^^ ...";
		next;
		callsub F_ChangeJob, (lastjob1+4001);
	}
	else if (Class > 4001 && Class < 4008)
	{
		callsub F_ReqBaseJobLevel, 40;
		mes "Você está quase chegando ao grau máximo de evolução.";
		mes "E então...";
		next;
		callsub F_ChangeJob, (lastjob+4001);
	}
		
	if (Class > 4023 && Class < 4030)
	{
		callsub F_ReqBaseJobLevel, 40;
		mes "Vejo que você voltou; e mais forte!";
		mes "Pois bem, que classe deseja seguir agora?";
		next;
		switch (Class)
		{
			case 4024:
				setarray .@c[0], 4030, 4037;
				break;
				
			case 4025:
				setarray .@c[0], 4032, 4039;
				break;
				
			case 4026:
				setarray .@c[0], 4034, ((Sex)?4042:4043);
				break;
				
			case 4027:
				setarray .@c[0], 4031, 4038;
				break;
				
			case 4028:
				setarray .@c[0], 4033, 4041;
				break;
			
			case 4029:
				setarray .@c[0], 4035, 4040;
				break;
		}
		select (jobname(.@c[0])+"",""+jobname(.@c[1]));
		callsub F_ChangeJob, .@c[@menu-1];
	}
	if (Class > 4007 && Class < 4022)
	{
		callsub F_ReqBaseJobLevel, 70, BaseLevel;
		mes "Nossa, como você está esplêndid"+(Sex?"o":"a")+"!";
		mes "Deseja chegar ao estado máximo de evolução em sua profissão?";
		mes "^FF0000Debug: relogue após se transformar para evitar transtornos.^000000";
		next;
		if (select ("Não:Sim") == 1)
			close;
		setarray .@ard[0], 4060, 4063, 4061, 4064, 4062, 4065, 4073, 4077, 4074, 4079, 4078, 4075, 4076;
		set .@rd, .@ard[(Class-((Class < 4014)?4008:4009))];
		callsub F_ChangeJob, .@rd;
	}

	S_MesClose:
		mes getarg(0);
		close;
	return;
			
	F_ChangeJob:
		mes "[Mestra das Classes]";
		if (BaseLevel < 45 && (getarg(0) == 23 || getarg(0) == 4045))
			callsub S_MesClose, "Você precisa ter pelo menos Nível de Base 45 para poder mudar de classe.";
		mes "Você tem certeza que deseja se tornar "+jobname(getarg(0))+"?";
		if (select ("Não:Sim") == 1)
			close;
		next;
		jobchange getarg(0);
		mes "[Mestra das Classes]";
		callsub S_MesClose, "Parabéns, agora você é um ^0000cd"+jobname(getarg(0))+"^000000!";
	return;
	
	F_ReqBaseJobLevel:
		if (getarg(1,99) < 99 || JobLevel < getarg(0))
			callsub S_MesClose, "Você precisa ter pelo menos "+((getarg(1,0))?"Nível de Base 99 e ":"")+"Nível de Classe "+getarg(0)+" para poder mudar de classe.";
	return;
}