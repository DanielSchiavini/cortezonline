//===== Cronus Script ========================================
//= Guerra do Emperium - Início Automático da GdE
//===== Por: =================================================
//= kalen (1.0)
//= 1.1 by Akaru and ho|yAnge|
//===== Versão Atual: ========================================
//= 2.5b
//===== Compatível com: ======================================
//= Cronus SVN; Episódio RO 4+
//===== Descrição: ===========================================
//= Início Automático da Guerra do Emperium
//= Para configuração do mesmo, leia por favor o arquivo em doc\woe_time_explanation.txt
//= Tempo de Início/Fim da GdE
//===== Comentários Adicionais: ==============================
//= 2.0 Trazido do eAthena. [Asus]
//= 2.1 Adicionado announce oficial do bRO. [SoulBlaker]
//= 2.2 Modificação de configuração de horário e preparamento para quests futuras. [Reply.]
//= 2.2a Correção no Script, não pode usar duas labels iguals. [RoM]
//= 2.3 Reestruturado. [RoM]
//= 2.3b Alterado de label para função [Heaven]
//= 2.4 Adicionado o suporte ao Log da WoE para broadcasts. [RoM]
//= 2.5a Corrigido pequeno erro de sintaxe nos callsubs. [Wilk Maia]
//= 2.5b Pequenas modificações aleatórias. [Mara]
//============================================================
-	script	Agit_Event	-1,{
	end;

OnInit:
OnMinute00:
OnTimer60000:
	stopnpctimer;
	if (gettime(3) == $agit_hora_start[gettime(4)] && gettime(2) == $agit_min_start[gettime(4)] && !agitcheck())
	{
		announce "O Período de Guerras começou!", 8;
		announce "Desejamos boa sorte a todos!", 8;
		agitstart;
		callsub OnDisplayOwners;
	}
	else if (gettime(3) == $agit_hora_end[gettime(4)] && gettime(2) == $agit_min_end[gettime(4)] && agitcheck())
	{
		announce "Neste momento está encerrado o Período de Guerras.", 8;
		announce "Obrigado a todos que participaram!", 8;
		agitend;
		callsub OnDisplayOwners;
	}
	initnpctimer;
	end;
	
	
	OnDisplayOwners:
		setarray .@maps$[0],"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05",	// Feudo de Aldebaran
							"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",		// Feudo de Geffen
							"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",		// Feudo de Payon
							"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05";		// Feudo de Prontera
		for (set .@i, 0; .@i < getarraysize(.@maps$); set .@i, .@i + 1)
		{
			if (getcastledata(.@maps$[.@i],1))
				announce "O Castelo de [" + getcastlename(.@maps$[.@i]) + "] foi conquistado pelo Clã [" + getguildname(getcastledata(.@maps$[.@i],1)) + "].", bc_blue|bc_woe;
			else
				announce "O Castelo de [" + getcastlename(.@maps$[.@i]) + "] está totalmente desocupado.", bc_blue|bc_woe;
		}
	return;
}